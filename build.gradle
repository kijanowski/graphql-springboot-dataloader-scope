plugins {
  id 'org.springframework.boot' version '2.1.8.RELEASE'
  id 'io.spring.dependency-management' version '1.0.8.RELEASE'
  id 'java'
  id "io.github.kobylynskyi.graphql.codegen" version "1.2.2"
}

group = 'graphqlscope-graphql'
version = '0.0.1-SNAPSHOT'
sourceCompatibility = '12'

repositories {
  mavenCentral()
}

dependencies {
  compile 'com.graphql-java-kickstart:graphiql-spring-boot-starter:5.2'
  compile 'com.google.guava:guava:28.1-jre'

  implementation 'com.graphql-java:graphql-java:13.0'
  implementation 'com.graphql-java:graphql-java-spring-boot-starter-webmvc:1.0'
  implementation 'org.springframework.boot:spring-boot-starter-web'
}

sourceSets {
  main {
    java {
      srcDirs = ['src/main/java', 'src/main/graphqlGen']
    }
  }
}

task cleanGraphqlGen {
  doFirst {
    delete("$projectDir/src/main/graphqlGen")
  }
}

clean {
  dependsOn 'cleanGraphqlGen'
}

compileJava.dependsOn graphqlCodegen

graphqlCodegen {
  graphqlSchemaPaths = ["$projectDir/src/main/resources/schema.graphqls".toString()]
  outputDir = new File("$projectDir/src/main/graphqlGen")
  apiPackageName = "graphqlscope.graphql.api"
  modelPackageName = "graphqlscope.graphql.model"
  customTypesMapping = [
        DateTime: "java.util.Date"
  ]
  modelNameSuffix = "TO"
  generateApis = true
}
